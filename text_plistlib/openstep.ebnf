# TatSu EBNF grammar for ASCII OpenStep/GNUStep property lists.
# @see https://developer.apple.com/library/archive/documentation/Cocoa/Conceptual/PropertyLists/OldStylePlists/OldStylePLists.html
# @see http://www.gnustep.org/resources/documentation/Developer/Base/Reference/NSPropertyList.html
# @see https://github.com/gnustep/libs-base/blob/master/Source/NSPropertyList.m
@@grammar::Plist
@@comments :: ?'/\*.*?\*/'
@@eol_comments :: ?'//.*?$'
@@nameguard :: False

# s: strings
# v: vanilla value
start = (v:value $) | (s:{ entry } $);

# Nullable entries are found in strings files.
# Since Python has the concept of None, we handle it here differently.
# http://www.gnustep.org/resources/documentation/Developer/Base/Reference/NSString.html#method$NSString-propertyListFromStringsFileFormat
entry::Entry = k:string [ '=' v:value ] ';';

value = dict | array | string | hexdata | base64data | typed;
dict::DictType = '{' @:{ entry } '}';
array::ArrayType = '(' @:','.{ value } [ ',' ]')'; # None value is my extension.
hexdata::BinType = '<' @:{ hexpairs } '>';
base64data::BinType = '<[' @:/[^\]]*/ ']>'; # gnustep
typed = '<*' @:typed_belly '>'; # gnustep

hexpairs = ?'(?i)([0-9a-f]{2})+';

# gsQuotable
# . (\x2E) is accepted when reading, but Apple CF quotes it when writing.
# That might have something to do with plutil's keypath.
safechar = ?'[-#!$%&*+./0-9:?@A-Z^_a-z|~]+';

# string
string::StringType = (sc:safechar) | ('"' ~ ec:{ qchar } '"' ~);

qchar = r:(/[^"\\]+/) | e:(?'\\' esc_seq);
esc_seq = ?'[abtrnvf]'
        | (?'(?i)u' ?'(?i)[0-9a-f]{4}')
        | (?'x' ?'(?i)[0-9a-f]{2}')
        | ?'[0-7]{,3}'
        | ?'.'
        ;

# GNUStep typed
number = /[0-9]+/;
int::int = [ '-' ] number;
uid = number;
float::float = [ '-' ] ( /(?i)nan/ | /(?i)inf/ |
    (number [ '.' [number] ] | '.' number) [ 'e' [ '[+-]' number ] ]
 );
date::DateType = /[^>"]+/;
bool = ?'[YN]';
nil = ?'[N]';

# Use Regexp here to skip nameguard
# Quoted values are tolerated by GNUstep
typed_belly = 
    ?'I' [?'"'] @:int [?'"'] |
    ?'U' [?'"'] @:uid [?'"'] | # my extension
    ?'R' [?'"'] @:float [?'"'] |
    ?'B' [?'"'] @:bool [?'"'] |
    ?'D' [?'"'] @:date [?'"'] | # 2006-01-02 15:04:05 -0700
    @:nil; # null/None/nil, my extension